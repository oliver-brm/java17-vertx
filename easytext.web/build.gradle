plugins {
    id 'java'
    id 'application'
    id "org.beryx.jlink" version "2.24.2"
}

dependencies {
    implementation "io.vertx:vertx-core:$vertxVersion"
    implementation project(':easytext.pagefetch')
    implementation project(':easytext.vertx')
    implementation project(':easytext.algorithm.api')

    runtimeOnly "io.netty:netty-transport-native-kqueue:$nettyVersion"
    runtimeOnly project(':easytext.algorithm.coleman')
    runtimeOnly project(':easytext.algorithm.kincaid')
    runtimeOnly project(':easytext.algorithm.naivesyllablecounter')

}

java {
    modularity.inferModulePath = true
}
application {
    mainClass = "javamodularity.easytext.web.Main"
    mainModule = "easytext.web"
}
jlink {
    enableCds()
    mergedModule {
        mergedModuleName = "easytext.mergedmodule"
        uses "io.vertx.core.spi.VerticleFactory"
        uses "io.vertx.core.spi.VertxServiceProvider"
        excludeProvides servicePattern: "reactor.blockhound.integration.*"
    }
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'easytext'
        customImage {
            appModules = appModules + [
                    "easytext.web",
                    "easytext.vertx",
                    "easytext.pagefetch",
                    "easytext.algorithm.api",
                    "easytext.algorithm.coleman",
                    "easytext.algorithm.kincaid",
                    "easytext.algorithm.naivesyllablecounter",
                    "jdk.crypto.ec",    // required for Netty to do TLS
                    "jdk.naming.dns",   // required for Netty, e.g. to look up the default DNS
                    "java.management",  // required for Netty, e.g. to look up the current PID
                    jlink.mergedModuleName.get()]
        }
        //jvmArgs = ['-Dlog4j.configurationFile=./log4j2.xml']
    }
}
